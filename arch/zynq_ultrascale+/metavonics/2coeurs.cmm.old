; Developed by Diogo Cochicho under Youcef Bouchebaba's supervision

; clear & powercycle
WinCLEAR
;DO "&scripts/PowerCycle.cmm"

&ppd=OS.PPD()

; --------------------------------------------------------------------------------
; check prerequisites
IF VERSION.BUILD.BASE()<75524.
(
  PRINT %ERROR "Please use more recent Software! Contact support@lauterbach.com."
  ENDDO
)

; --------------------------------------------------------------------------------
; initialize and start the debugger
RESet
SYStem.RESet

Intercom.name A53_0
TargetSystem.NewInstance A53_1 /arch ARM

SYStem.CPU ZYNQ-ULTRASCALE+-APU
SYStem.Option TRST OFF
SYStem.Option ResBreak OFF
SYStem.Option WaitReset 500ms
SYStem.JtagClock CTCK 10MHz
CORE.ASSIGN 1.
; disable Trace infrastructure for the moment - TPIU may be unclocked!
SYStem.Mode Prepare

InterCom A53_1 SYStem.CPU ZYNQ-ULTRASCALE+-APU
InterCom A53_1 SYStem.Option TRST OFF
InterCom A53_1 SYStem.Option ResBreak OFF
InterCom A53_1 SYStem.Option WaitReset 500ms
InterCom A53_1 SYStem.JtagClock CTCK 10MHz
InterCom A53_1 SYStem.CONFIG Core 2. 1.
InterCom A53_1 CORE.ASSIGN 2.

DO "&ppd/zynq_ultrascale_kickboot.cmm" A53_X64
DO "&ppd/zynq_ultrascale_kickboot.cmm" _A53_234_X64

ETM.OFF
STM.OFF
Trace.DISable
InterCom A53_1 ETM.OFF
InterCom A53_1 STM.OFF
InterCom A53_1 Trace.DISable

SYStem.Mode.Attach
InterCom A53_1 SYStem.Mode.Attach
IF STATE.RUN()
  Break.direct

; --------------------------------------------------------------------------------
; load the FSBL (first-stage boot loader) into the OCM
Data.LOAD.Elf "&ppd/Meta/zynqmp_fsbl/fsbl_a53.elf"

; --------------------------------------------------------------------------------
; start FSBL execution
Go.direct XFsbl_Loop
WAIT !STATE.RUN() 3.s
;IF STATE.RUN()
;(
;  Break.direct
;)

IF Register(PC)!=ADDRESS.OFFSET(XFsbl_Loop)
(
  PRINT %ERROR "Boot flow error"
  ENDDO
)

; The FSBL configures the trace clock to 250 Mhz, which is too much for the
; single-lane design. Manually switch to a lower clock. Note: You do not need
; to do this if you regenerate the FSBL to match the project.
;Data.Set AD:0xFF5E00C4 %Long 0x1010802 ; 1200 MHz clock source divided by (1 * 8) => trace clock = 150 MHz

; --------------------------------------------------------------------------------
InterCom A53_1 Break
InterCom A53_1 DO "&ppd/loadapp.cmm" "&ppd/Core1/Debug/Core1.elf"
InterCom A53_0 Break
InterCom A53_0 DO "&ppd/loadapp.cmm" "&ppd/Core0/Debug/Core0.elf" TRUE()
